group 'pm.hhp.api'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'checkstyle'

sourceCompatibility = 1.8

repositories {
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

dependencies {
  compile group: 'com.github.HHPnet', name: 'jcore', version: '0.1.3'

  compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
  compile group: 'org.springframework.security.oauth', name: 'spring-security-oauth2'
  compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-feign', version: '1.2.3.RELEASE'

  compile group: 'org.postgresql', name: 'postgresql', version: '9.4.1212.jre7'
  compile group: 'org.flywaydb', name: 'flyway-core', version: '4.0.3'

  compile group: 'io.jsonwebtoken', name: 'jjwt', version: '0.7.0'
  compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'

  testCompile group: 'junit', name: 'junit', version: '4.12'
  testCompile group: 'org.assertj', name: 'assertj-core', version: '3.6.1'
  testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'

  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
  testCompile group: 'org.springframework.security', name: 'spring-security-test'

  testCompile group: 'info.cukes', name: 'cucumber-spring', version: '1.2.5'
  testCompile group: 'info.cukes', name: 'cucumber-junit', version: '1.2.5'

  testCompile group: 'org.skyscreamer', name: 'jsonassert', version: '1.4.0'
}

buildscript {
  repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://plugins.gradle.org/m2/' }
  }
  dependencies {
    classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.10'
    classpath 'gradle.plugin.net.vivin:gradle-semantic-build-versioning:2.0.2'
    classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.4.3.RELEASE'
    classpath 'com.avast.gradle:docker-compose-gradle-plugin:0.3.15'
  }
}

apply plugin: 'info.solidsoft.pitest'
apply plugin: 'net.vivin.gradle-semantic-build-versioning'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'docker-compose'

checkstyle {
  configFile = file('checkstyle.xml')
  toolVersion = '7.1.2'
  showViolations = true
  ignoreFailures = false
}

sourceSets {
  main.java.srcDirs = ['src/main/java']
  test.java.srcDirs = ['src/test/unit', 'src/test/cucumber']
}

configurations {
  cucumberRuntime {
    extendsFrom testRuntime
  }
}

task cucumber() {
  dependsOn assemble, compileTestJava, composeUp
  finalizedBy composeDown
  doLast {
    javaexec {
      main = "cucumber.api.cli.Main"
      classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
      args = ['--plugin', 'pretty', '--glue', 'pm.hhp.api.tests.cucumber', 'src/test/resources']
      jvmArgs = ["-Dspring.profiles.active=test"]
    }
  }
}